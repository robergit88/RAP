//*&---------------------------------------------------------------------*
//*& ID_PROGRAMA: zi_rap_booking_1234                                    *
//*&                                                                     *
//*& TIPO DE PROGRAMA: INTERFAZ CDS                                      *
//*&                                                                     *
//*& DESCRIPCION: CDS de BASE sobre ella se construyen proyecciones      *
//*&                                                                     *
//*& AUTOR: CB9980000068 - Roberto Puma                                  *
//*&                                                                     *
//*& FECHA DE CREACIÃ“N: 01.10.2025                                       *
//*&                                                                     *
//*&---------------------------------------------------------------------*
//*&                                                                     *
//*& HISTORIAL DE CAMBIOS                                                *
//*&                                                                     *
//*&  FECHA      AUTOR        MARCA                                      *
//*&  ------     ----------   -------------                              *
//*& DD.MM.AAAA  XXXXXXX      001                                        *
//*&                                                                     *
//*&---------------------------------------------------------------------*
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Interfaz CDS de Reservas'
@Metadata.allowExtensions: true


define view entity zi_rap_booking_1234
  as select from zrap_abook_1234 as Booking

  association        to parent zi_rap_travel_1234 as _Travel     on  $projection.TravelUUID = _Travel.TravelUUID

  association [1..1] to /DMO/I_Customer           as _Customer   on  $projection.CustomerID = _Customer.CustomerID
  association [1..1] to /dmo/carrier              as _Carrier    on  $projection.CarrierID = _Carrier.carrier_id
  association [1..1] to /DMO/I_Connection         as _Connection on  $projection.CarrierID    = _Connection.AirlineID
                                                                 and $projection.ConnectionID = _Connection.ConnectionID
  association [1..1] to /DMO/I_Flight             as _Flight     on  $projection.CarrierID    = _Flight.AirlineID
                                                                 and $projection.ConnectionID = _Flight.ConnectionID
                                                                 and $projection.FlightDate   = _Flight.FlightDate
  association [0..1] to I_Currency                as _Currency   on  $projection.CurrencyCode = _Currency.Currency


{
  key booking_uuid          as BookingUUID,
      travel_uuid           as TravelUUID,
      booking_id            as BookingID,
      booking_date          as BookingDate,
      customer_id           as CustomerID,
      carrier_id            as CarrierID,
      connection_id         as ConnectionID,
      flight_date           as FlightDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      flight_price          as FlightPrice,
      currency_code         as CurrencyCode,
      @Semantics.user.createdBy: true
      created_by            as CreatedBy,
      @Semantics.user.lastChangedBy: true
      last_changed_by       as LastChangedBy,
      @Semantics.systemDateTime.localInstanceLastChangedAt: true
      local_last_changed_at as LocalLastChangedAt,

      /* associations */
      _Travel,
      _Customer,
      _Carrier,
      _Connection,
      _Flight,
      _Currency
}
