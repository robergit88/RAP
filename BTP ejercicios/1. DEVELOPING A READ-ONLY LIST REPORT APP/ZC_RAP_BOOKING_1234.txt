//*&---------------------------------------------------------------------*
//*& ID_PROGRAMA: ZC_RAP_BOOKING_1234                                    *
//*&                                                                     *
//*& TIPO DE PROGRAMA: Vista CONSUMO CDS                                 *
//*&                                                                     *
//*& DESCRIPCION: Vista proyección                                       *
//*&                                                                     *
//*& AUTOR: CB9980000068 - Roberto Puma                                  *
//*&                                                                     *
//*& FECHA DE CREACIÓN: 01.10.2025                                       *
//*&                                                                     *
//*&---------------------------------------------------------------------*
//*&                                                                     *
//*& HISTORIAL DE CAMBIOS                                                *
//*&                                                                     *
//*&  FECHA      AUTOR        MARCA                                      *
//*&  ------     ----------   -------------                              *
//*& DD.MM.AAAA  XXXXXXX      001                                        *
//*&                                                                     *
//*&---------------------------------------------------------------------*
@AccessControl.authorizationCheck: #NOT_REQUIRED

@EndUserText.label: 'Vista de CONSUMO de reservas'

@Metadata.allowExtensions: true

@Search.searchable: true
//@ObjectModel.usageType: #CONSUMPTION

define view entity ZC_RAP_BOOKING_1234
  as projection on zi_rap_booking_1234

{
  key BookingUUID,

      TravelUUID,

      @Search.defaultSearchElement: true
      BookingID,

      BookingDate,

      @Consumption.valueHelpDefinition: [ { entity: { name: '/DMO/I_Customer', element: 'CustomerID' } } ]
      @ObjectModel.text.element: [ 'CustomerName' ]
      @Search.defaultSearchElement: true
      CustomerID,

      _Customer.LastName  as CustomerName,

      @Consumption.valueHelpDefinition: [ { entity: { name: '/DMO/I_Carrier', element: 'AirlineID' } } ]
      @ObjectModel.text.element: [ 'CarrierName' ]
      CarrierID,

      _Carrier.name       as CarrierName,

      @Consumption.valueHelpDefinition: [ { entity: { name: '/DMO/I_Flight', element: 'ConnectionID' },
                                            additionalBinding: [ { localElement: 'CarrierID', element: 'AirlineID' },
                                                                 { localElement: 'FlightDate',
                                                                   element: 'FlightDate',
                                                                   usage: #RESULT },
                                                                 { localElement: 'FlightPrice',
                                                                   element: 'Price',
                                                                   usage: #RESULT },
                                                                 { localElement: 'CurrencyCode',
                                                                   element: 'CurrencyCode',
                                                                   usage: #RESULT } ] } ]
      ConnectionID,

      FlightDate,

      @Semantics.amount.currencyCode: 'CurrencyCode'
      FlightPrice,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_Currency', element: 'Currency' } } ]
      CurrencyCode,

      LocalLastChangedAt,

      /* associations */
      _Travel : redirected to parent ZC_RAP_TRAVEL_1234,

      _Customer,
      _Carrier,
      _Connection,
      _Flight
}
